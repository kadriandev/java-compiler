/*
  Created by: Fei Song
  File Name: tiny.cup
  To Build: java java_cup.Main < tiny.cup
*/
   
   
/* ----------------------Preliminary Declarations Section--------------------*/
   
/* Import the class java_cup.runtime.*  */
//import java_cup.runtime.*;
import absyn.*;
   
/* Parser code to change the way the parser reports errors (include
   line and column number of the error). */
parser code {:
    
    /* Change the method report_error so it will display the line and
       column of where the error occurred in the input as well as the
       reason for the error which is passed into the method in the
       String 'message'. */
    public void report_error(String message, Object info) {
   
        /* Create a StringBuffer called 'm' with the string 'Error' in it. */
        StringBuffer m = new StringBuffer("Error");
   
        /* Check if the information passed to the method is the same
           type as the type java_cup.runtime.Symbol. */
        if (info instanceof java_cup.runtime.Symbol) {
            /* Declare a java_cup.runtime.Symbol object 's' with the
               information in the object info that is being typecasted
               as a java_cup.runtime.Symbol object. */
            java_cup.runtime.Symbol s = ((java_cup.runtime.Symbol) info);
   
            /* Check if the line number in the input is greater or
               equal to zero. */
            if (s.left >= 0) {                
                /* Add to the end of the StringBuffer error message
                   the line number of the error in the input. */
                m.append(" in line "+(s.left+1));   
                /* Check if the column number in the input is greater
                   or equal to zero. */
                if (s.right >= 0)                    
                    /* Add to the end of the StringBuffer error message
                       the column number of the error in the input. */
                    m.append(", column "+(s.right+1));
            }
        }
   
        /* Add to the end of the StringBuffer error message created in
           this method the message that was passed into this method. */
        m.append(" : "+message);
   
        /* Print the contents of the StringBuffer 'm', which contains
           an error message, out on a line. */
        System.err.println(m);
    }
   
    /* Change the method report_fatal_error so when it reports a fatal
       error it will display the line and column number of where the
       fatal error occurred in the input as well as the reason for the
       fatal error which is passed into the method in the object
       'message' and then exit.*/
    public void report_fatal_error(String message, Object info) {
        report_error(message, info);
        System.exit(1);
    }

:};
   
   
/* ------------Declaration of Terminals and Non Terminals Section----------- */
   
terminal IF, THEN, ELSE, END, REPEAT, UNTIL, READ, WRITE;
terminal ASSIGN, EQ, LT, GT, PLUS, MINUS, TIMES, OVER, LPAREN, RPAREN, SEMI;
terminal String ID, NUM;
terminal ERROR;
   
non terminal Exp program1;
non terminal ExpList program, stmt_seq;
non terminal Exp stmt, if_stmt, repeat_stmt, assign_stmt, read_stmt, write_stmt;
non terminal Exp exp;   

precedence nonassoc LT, GT, EQ;
precedence left PLUS, MINUS;
precedence left TIMES, OVER;

/* ----------------------------Grammar Section-------------------- */
   
start with program;

program1     ::= exp:e 
                 {: RESULT = e; :}
               ;

program	     ::= stmt_seq:s
                 {: RESULT = s; :}
               ;

stmt_seq     ::= stmt_seq:s1 SEMI stmt:s2 
                 {: ExpList t = s1;
                    while( t.tail != null )
                      t = t.tail;
                    t.tail = new ExpList(s2, null);
                    RESULT = s1;
                 :}
               | stmt:s
                 {: RESULT = new ExpList(s, null); :}
               | error SEMI stmt:s
                 {: parser.report_error("invalid", null); 
                    RESULT = new ExpList(s, null); 
                 :}
               ;

stmt         ::= if_stmt:i
                 {: RESULT = i; :}
               | repeat_stmt:r
                 {: RESULT = r; :}
               | assign_stmt:a
                 {: RESULT = a; :}
               | read_stmt:r
                 {: RESULT = r; :}
               | write_stmt:w
                 {: RESULT = w; :}
               ;

if_stmt      ::= IF:i exp:e THEN stmt_seq:s END
                 {: RESULT = new IfExp(ileft, iright, e, s, null); :}
               | IF:i exp:e THEN stmt_seq:s1 ELSE stmt_seq:s2 END
                 {: RESULT = new IfExp(ileft, iright, e, s1, s2); :}
               ;

repeat_stmt  ::= REPEAT:r stmt_seq:s UNTIL exp:e
                 {: RESULT = new RepeatExp(rleft, rright, s, e); :}
               ;

assign_stmt  ::= ID:i ASSIGN exp:e
                 {: RESULT = new AssignExp(ileft, iright, new VarExp(ileft, iright, i), e); :} 
               ;

read_stmt    ::= READ:r ID:i
                 {: RESULT = new ReadExp(rleft, rright, new VarExp(rleft, rright, i)); :}
               ;

write_stmt   ::= WRITE:w exp:e
                 {: RESULT = new WriteExp(wleft, wright, e); :}
               ;

exp          ::= exp:s1 LT exp:s2
                 {: RESULT = new OpExp(s1left, s1right, s1, OpExp.LT, s2); :}
               | exp:s1 GT exp:s2
                 {: RESULT = new OpExp(s1left, s1right, s1, OpExp.GT, s2); :}
               | exp:s1 EQ exp:s2
                 {: RESULT = new OpExp(s1left, s1right, s1, OpExp.EQ, s2); :}
               | exp:s PLUS exp:t
                 {: RESULT = new OpExp(sleft, sright, s, OpExp.PLUS, t); :}
               | exp:s MINUS exp:t
                 {: RESULT = new OpExp(sleft, sright, s, OpExp.MINUS, t); :}
               | exp:t TIMES exp:f
                 {: RESULT = new OpExp(tleft, tright, t, OpExp.TIMES, f); :}
               | exp:t OVER exp:f
                 {: RESULT = new OpExp(tleft, tright, t, OpExp.OVER, f); :}
               | LPAREN exp:e RPAREN
                 {: RESULT = e; :}
               | NUM:n
                 {: RESULT = new IntExp(nleft, nright, n); :}
               | ID:i
                 {: RESULT = new VarExp(ileft, iright, i); :}
               ;
